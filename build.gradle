plugins {
  alias libs.plugins.kotlin.jvm
  alias libs.plugins.kover
}

allprojects {
  apply plugin: libs.plugins.kotlin.jvm.get().pluginId
  apply plugin: libs.plugins.kover.get().pluginId

  repositories {
    mavenCentral()
    mavenLocal()
  }

  group = groupId
  version = projectVersion

  kotlin {
    jvmToolchain(libs.versions.java.get().toInteger())
  }

  test {
    jvmArgs(
        "--add-opens", "java.base/java.time=ALL-UNNAMED",
        "--add-opens", "java.base/java.lang.reflect=ALL-UNNAMED",
    )
    useJUnitPlatform()
    finalizedBy(
        koverXmlReport,
        koverHtmlReport,
    )
  }

  kover {
    reports {
      filters {
        excludes {
          classes(mainClassName)
        }
      }
    }
  }
}

subprojects {
  apply plugin: libs.plugins.application.get().pluginId

  dependencies {
    implementation libs.bundles.implementation
    runtimeOnly libs.bundles.runtimeOnly
    testImplementation libs.bundles.test.implementation
  }
}

dependencies {
  kover(project(libModule))
}
